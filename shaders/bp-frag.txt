#include "preamble"

uniform float tmax;

uniform sampler2D radiance; // x time, y spad

uniform int useAbsolute; // To accumulate for the conventional camera
uniform float lightIsLaser; // 1.0 for true, 0.0 for false
uniform float isConfocalModel; // 1.0 for true, 0.0 for false
uniform float instant;
uniform vec2 laserPos;
uniform vec2 laserGrid; // could be more than one, actually
uniform vec2 spadPos;
uniform sampler2D spadGrid;

uniform sampler2D planeGrid; // Plane to reconstruct
        // positions of the considered pixels, on a row

varying vec2 mPos; // Pixel coordinates [0,1]

const int numSpads = {numSpads};

void main() {
    float spadDist = 1.0 / float(numSpads);
    float xSpad = spadDist / 2.0;

    vec2 pixelPos = texture2D(planeGrid, vec2(mPos.x, 1.0 - mPos.y)).xy;
    vec2 radianceAccum = vec2(0.0);
    for (int i = 0; i < numSpads; i++) {
        vec2 wallSpad = texture2D(spadGrid, vec2(xSpad, 0.5)).xy;
        float dlp = distance(laserGrid, laserPos);
        float dl  = distance(laserGrid, pixelPos);
        float dsp = distance(wallSpad, spadPos); // distance spad device to captured points
        float ds  = distance(wallSpad, pixelPos);
        float dt = ds + dsp + dl * isConfocalModel + dlp * lightIsLaser + instant;

        float t = dt / tmax;
        radianceAccum += texture2D(radiance, vec2(t, xSpad)).xy * vec2(t <= 1.0);
        xSpad += spadDist;
    }
    gl_FragColor = vec4(length(radianceAccum) * float(useAbsolute) + radianceAccum.x * float(1 - useAbsolute), 
            radianceAccum.y * float(1 - useAbsolute), 0.0, 1.0);
}
